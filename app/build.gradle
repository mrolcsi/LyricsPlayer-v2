apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'

apply from: "https://gist.githubusercontent.com/mrolcsi/588d5dd7a309fab501fc/raw/30b88f5217596c425c43d101dd29708a785b05f1/Version%2520code%2520and%2520name%2520generation%2520using%2520Gradle"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "hu.mrolcsi.muzik"
        minSdkVersion 21
        versionCode makeVersionCode()
        versionName makeVersionNameDebug()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/src/androidTest/schemas".toString()]
            }
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/src/androidTest/schemas".toString())
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
}
androidExtensions {
    experimental = true
}
dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    // Core libraries
    implementation 'androidx.core:core-ktx:1.2.0-alpha03'
    implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha03'
    implementation 'androidx.fragment:fragment-ktx:1.2.0-alpha02'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0-rc01'

    // Views and Layouts
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-beta02'
    implementation 'com.google.android.material:material:1.1.0-alpha09'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.l4digital.fastscroll:fastscroll:2.0.1'
    implementation 'com.afollestad.material-dialogs:core:3.1.0'

    // Media
    implementation 'androidx.media:media:1.1.0-rc01'

    // ExoPlayer (Use submodule for now)
    implementation project(':exoplayer-library-core')
    implementation project(':exoplayer-library-ui')
    implementation project(':exoplayer-extension-mediasession')
    implementation project(':exoplayer-extension-ffmpeg')
    implementation project(':exoplayer-extension-flac')

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    // Dagger
    implementation 'com.google.dagger:dagger:2.24'
    kapt 'com.google.dagger:dagger-compiler:2.24'
    implementation 'com.google.dagger:dagger-android:2.24'
    implementation 'com.google.dagger:dagger-android-support:2.24'
    kapt 'com.google.dagger:dagger-android-processor:2.24'

    // Rx
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0-beta.1'

    // Palette
    debugImplementation "androidx.palette:palette-ktx:1.0.0"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'

    // Custom Activity on Crash
    implementation 'cat.ereza:customactivityoncrash:2.2.0'

    // Checkerboard Drawable
    debugImplementation 'com.github.duanhong169:checkerboarddrawable:1.0.2'

    // Room
    implementation 'androidx.room:room-runtime:2.2.0-alpha02'
    kapt 'androidx.room:room-compiler:2.2.0-alpha02'

    // Retrofit & Gson
    implementation 'com.squareup.retrofit2:retrofit:2.6.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.1'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.1'

    // Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0-rc01'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha02'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha02'
    androidTestImplementation 'androidx.room:room-testing:2.2.0-alpha02'
}

